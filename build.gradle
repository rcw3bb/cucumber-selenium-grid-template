import org.apache.commons.io.output.ByteArrayOutputStream

import java.nio.file.Paths

apply plugin: 'java'

ext {
    cucumber_version = "4.2.0"
    selenium_version = "3.141.59"
}

sourceCompatibility = 8
targetCompatibility = 8

group 'xyz.ronella.testing.cucumber'
version '1.0-SNAPSHOT'

repositories {
    maven {
        url 'https://repo.ronella.xyz/artifactory/java-central'
        credentials {
            username "${artifactoryUsername}"
            password "${artifactoryPassword}"
        }
    }
}

buildscript {
    repositories {
        maven {
            url 'https://repo.ronella.xyz/artifactory/java-central'
            credentials {
                username "${artifactoryUsername}"
                password "${artifactoryPassword}"
            }
        }
    }
    dependencies {
        classpath group: 'commons-io', name: 'commons-io', version: '2.6'
    }
}

configurations.all {
    resolutionStrategy {
        force group: 'io.cucumber', name: 'cucumber-core', version: "${cucumber_version}"
    }
}

dependencies {
    implementation group: 'org.seleniumhq.selenium', name: 'selenium-support', version: "${selenium_version}"

    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-server-standalone', version: "${selenium_version}"
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: "${selenium_version}"
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-chrome-driver', version: "${selenium_version}"

    testImplementation group: 'io.cucumber', name: 'cucumber-java', version: "${cucumber_version}"
    testImplementation group: 'io.cucumber', name: 'cucumber-junit', version: "${cucumber_version}"

    testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: '2.0.82'
    testImplementation group: 'net.serenity-bdd', name: 'serenity-cucumber4', version: '1.0.21'
}

task aggregate {
    doLast {
        def output = new ByteArrayOutputStream()
        exec {
            executable 'mvn.cmd'
            args 'serenity:aggregate'
            standardOutput = output
        }
        print output
    }
}

task startSeleniumHub(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    main = 'org.openqa.grid.selenium.GridLauncherV3'
    args = ['-role', 'hub']
}

task startSeleniumNode(type: JavaExec)  {
    def pathGeckoWebDriver = Paths.get(projectDir.toString(), 'webdriver', 'geckodriver.exe').toString()

    classpath = sourceSets.test.runtimeClasspath
    main = 'org.openqa.grid.selenium.GridLauncherV3'
    jvmArgs = ["-Dwebdriver.gecko.driver=${pathGeckoWebDriver}"]
    args = ['-role'
            , 'node'
            , '-hub'
            , 'http://localhost:4444/grid/register'
    ]
}

test.finalizedBy(aggregate)